;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; TYPER ; Data and String Handler ;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;:### typ.ffw (pointer, word)
;:**F**inds the **f**irst **w**ord matching *word* after *pointer*, and returns the number of words between *pointer* and that *word*.
;:
;:**modifies** A
;:
:typ.ffw
SET PUSH, A

:typ.ffw.loop
IFE [A], B
  SET PC, typ.ffw.exit

ADD A, 1
SET PC, typ.ffw.loop

:typ.ffw.exit
SUB A, POP
SET PC, POP
 ;

;:### typ.ffz (pointer)
;:**F**inds the **f**irst **z**ero after *pointer*, and returns the number of words between *pointer* and that zero.
;:
;:**modifies** A
;:
:typ.ffz
SET PUSH, B
SET B, 0
JSR typ.ffw ;find first word matching 0
SET B, POP
SET PC, POP

;:### typ.htoascii (hextet, outputPointer)
;:Converts *hextet* (a 16-bit unsigned integer) to ASCII, and writes that ASCII to *outputPointer*. Always produces four ASCII characters.
;:
;:**returns** the location following the last character of the ASCII writing.
;:
:typ.htoascii
SET PUSH, I
SET I, 4 ;counter for how many times to shift the hextet

:typ.htoascii.lookup
SET C, 0xf000
AND C, A
SHR C, 12
SET [B], [typ.htoascii.table + C]

SHL A, 4
SUB I, 1
ADD B, 1

IFN I, 0
  SET PC, typ.htoascii.lookup

SET I, POP
SET A, B
SET PC, POP

:typ.htoascii.table
DAT 0x30, 0x31, 0x32, 0x33, 0x34, 0x35, 0x36, 0x37, 0x38, 0x39 ; "0" - "9"
DAT 0x61, 0x62, 0x63, 0x64, 0x65, 0x66 ; "a" - "f"
 ;

;:### typ.asciitoh (inputPointer)
;:Scans a 4-character string in hex form and produces a single word output containing the value that string represents.
;:
;:**returns** the value represented by the hex string
;:
;:**author** lukevers
;:
:typ.asciitoh
SET PUSH, I
SET PUSH, X

SET I, 12
SET X, 0

SET C, A

:typ.asciitoh.loop
SET A, typ.htoascii.table
SET B, [C]
BOR B, 0x0020
JSR typ.ffw

SHL A, I
BOR X, A

ADD C, 1
SUB I, 4

IFA I, 0xffff ;if I is greater than than -1, signed (so that the last bit is examined)
  SET PC, typ.asciitoh.loop

SET A, X
SET X, POP
SET I, POP
SET PC, POP

;:### typ.cCopy (inputPointer, outputPointer)
;:Copies the null-terminated string at *inputPointer* to *outputPointer*.
;:**returns** the location following the last character of the copy at the *outputPointer* location
;:

:typ.cCopy
SET PUSH, I
SET PUSH, J
SET I, A
SET J, B

:typ.cCopy.loop
IFE [I], 0
  SET PC, typ.cCopy.exit

STI [J], [I]
SET PC, typ.cCopy.loop

:typ.cCopy.exit
SET [J], 0
SET A, J
SET J, POP
SET I, POP
SET PC, POP
 ;

;:### typ.pCopy (inputPointer, outputPointer, length)
;:Copies data of *length* from *inputPointer* to *outputPointer*.
;:**returns** a pointer to the word following the last output word
;:
:typ.pCopy
SET PUSH, I
SET PUSH, J

SET I, A
SET J, B
ADD C, A

:typ.pCopy.loop
IFE I, C
  SET PC, typ.pCopy.exit

STI [J], [I]
SET PC, typ.pCopy.loop

:typ.pCopy.exit
SET A, J
SET J, POP
SET I, POP
SET PC, POP
 ;

;:### typ.cUnpack (inputPointer, outputPointer)
;:Reads a packed (`0b0YYY YYYY 0XXX XXXX`) null-terminated at *inputPointer*(A) and outputs an unpacked (`0b0000 0000 0XXX XXXX`) string at *outputPointer*(B).
;:
;:**returns** the null location after the last unpacked character
;:

:typ.cUnpack
SET PUSH, I
SET I, A
SET C, B

:typ.cUnpack.loop
IFE [I], 0
  SET PC, typ.cUnpack.exit
 
set push, [I]
and [I], 0x007F
IFE [I], 0
  SET PC, typ.cUnpack.exit
set [C], [I]
set [I], pop
add c, 1
set push, [I]
shr [I], 8
and [I], 0x007F
IFE [I], 0
  SET PC, typ.cUnpack.exit
set [C], [I]
set [I], pop
ADD I, 1
add C, 1
SET PC, typ.cUnpack.loop

:typ.cUnpack.exit
set [c], 0
SET A, C
SET I, POP
SET PC, pop

;:### typ.pPack (inputLocation, inputLength, outputLocation)
;:Reads an unpacked (`0b0000 0000 0XXX XXXX`) string from *inputLocation* of *inputLength* and outputs a packed (`0b0YYY YYYY 0XXX XXXX`) little-endian style string at *outputLocation*.
;:

:typ.pPack
SET PUSH, I
SET PUSH, X
SET PUSH, Y
SET PUSH, Z

SET Z, 0x007F
SET I, A
ADD B, A


:typ.pPack.loop
STI X, [I]
AND X, Z
SET [C], X
STI X, [I]
AND X, Z
SHL X, 8
XOR [C], X

ADD C, 1
IFL I, B
  SET PC, typ.pPack.loop

SET Z, POP
SET Y, POP
SET X, POP
SET I, POP
SET PC, POP
 ;

;:### typ.pUnpack (inputPointer, inputLength, outputPoiner)
;:Reads a packed (`0b0YYY YYYY 0XXX XXXX`) in little-endian at *inputPointer* of *inputLength* and outputs an unpacked (`0b0000 0000 0XXX XXXX`) string at *outputPointer*.
;:

:typ.pUnpack
SET PUSH, I
SET PUSH, X
SET PUSH, Y
SET PUSH, Z

SET I, C
ADD B, A
SET Z, 0x007F
SET Y, 0x7F00

:typ.pUnpack.loop
SET X, [A]
AND X, Z
STI [I], X

SET X, [A]
AND X, Y
SHR X, 8
STI [I], X

ADD A, 1
IFL A, B
  SET PC, typ.pUnpack.loop

SET A, I ;return in A

SET Z, POP
SET Y, POP
SET X, POP
SET I, POP
SET PC, POP
 ;

;:### typ.memcopy (source, destination, length)
;:Copies `length` words from `source` to `destination`.
;:Correctly handles overlapping regions. Returns nothing.
;:

:typ.memcopy
SET PUSH, X

; Check first if there is overlap.
IFG A, B ; Destination before source, no danger.
  SET PC, typ.memcopy.copyForwards

; Check if the source extends into the destination.
SET X, A
ADD X, C
SUB X, 1 ; X is now the last value of the source.
IFG B, X ; No problem, destination does not overlap.
  SET PC, typ.memcopy.copyForwards

; If we arrive here, we need to copy backwards.
ADD A, C
SUB A, 1 ; A now points at the end of the source.
ADD B, C
SUB B, 1 ; And B at the end of the destination.

SET X, C
:typ.memcopy.backwardLoop
SET [B], [A]
SUB A, 1
SUB B, 1
SUB X, 1
IFG X, 0
  SET PC, typ.memcopy.backwardLoop

SET PC, typ.memcopy.done

:typ.memcopy.copyForwards
SET [B], [A]
ADD A, 1
ADD B, 1
SUB C, 1
IFG C, 0
  SET PC, typ.memcopy.copyForwards

:typ.memcopy.done
SET X, POP
SET PC, POP


;:### typ.memfill (startPointer, length, value)
;:Fills *length* words with *value*, starting at *startPointer*
;:

:typ.memfill
SET PUSH, X
SET X, A
ADD X, B
:typ.memfill.loop
SET [A], C
ADD A, 1
IFN A, X
SET PC, typ.memfill.loop
SET X, POP
SET PC, POP
 ;

;:### typ.memzero (startPointer, length)
;:Fills *length* words with 0, starting at *startPointer*
;:

:typ.memzero
SET C, 0
SET PC, typ.memfill
 ;
 ;;

;:### typ.asciitod (inputPointer)
;:Reads decimal digits from the null-terminated ASCII string *inputPointer*. Returns the value of that decimal number in A.
;:Returns 0 on empty string.
;:Does NOT check for overflow, but it won't crash if fed too large a number (> 65535)
;:Doesn't check that the characters in the string are actually digits.
;:Reads the value unsigned, and doesn't expect a -.
;:

:typ.asciitod
; Advance A through the list. Keep a running total in C.
SET PUSH, C
SET PUSH, X

SET C, 0

:typ.asciitod.loop
SET X, [A]
IFE X, 0
  SET PC, typ.asciitod.done

SUB X, 0x30 ; turns ASCII 0 into actual 0, etc.
MUL C, 10
ADD C, X
ADD A, 1
SET PC, typ.asciitod.loop

:typ.asciitod.done
SET A, C
SET X, POP
SET C, POP
SET PC, POP



;:### typ.dtoascii (word, outputBuffer)
;:Writes the (unsigned) value of *word* to *outputBuffer* as ASCII.
;:Returns the position after the last character in *outputBuffer*.
;:Reads the value unsigned, and doesn't expect a -.
;:

:typ.dtoascii
; Repeatedly divide and mod the number, pushing ASCII values to the stack, then popping and outputting.
SET PUSH, C

SET PUSH, 0xffff ; sentinel value for the end of the digits

:typ.dtoascii.divloop
SET C, A
MOD C, 10
SET PUSH, C
DIV A, 10
IFG A, 0
  SET PC, typ.dtoascii.divloop

; Now the output phase begins. Pop to A and write it until it's 0xffff.
:typ.dtoascii.outloop
SET A, POP
IFE A, 0xffff
  SET PC, typ.dtoascii.done
ADD A, 0x30 ; convert to an ASCII digit.
SET [B], A
ADD B, 1
SET PC, typ.dtoascii.outloop

:typ.dtoascii.done
SET A, B
SET C, POP
SET PC, POP

