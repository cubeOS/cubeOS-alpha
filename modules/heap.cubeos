;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; HEAPMAN ; Heap Manager   ;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;:# Heap Manager
;:

;:### heap.init
;:(location, length, blocksize)
;:heap.init reserves a 4 word header followed by a bit map of all blocks. The size of the bit map is (*maxlength*/*blocksize*)/16. *length* is actually the length of the heap, excluding the header and bit map.
;:
;:The header consists of a magic word, **0x9ea9**, the location of the first block in memory, the size of each block, and the maximum length of the heap.
;:
;:**This subroutine clears all of the space given to it.**
;:
;:**returns** location of header
;:

:heap.init
SET PUSH, I
SET PUSH, J

SET [env.heap], A

SET I, A
SET PUSH, A ;store the location for returning
SET A, B

DIV A, C ;find the number of necessary blocks
IFN EX, 0
  ADD A, 1 ;round up

         ;make A the number of words needed for the bitmap:
SHR A, 4 ;divide by 16
IFE EX, 0
  SET PC, heap.init.evenbitmap ;if that goes evenly, skip the next step

ADD A, 1 ;round up
SET PUSH, A ;for restoring at the end of this part
SET PUSH, B ;for use in this part

ADD A, I ;add the heap offset
ADD A, 4 ;add the size of the header
         ;to make A the location of the last word in the bitmap

SET [A], 0xffff ;set it to be filled
SET B, PICK 2   ;set B to the number of blocks
AND B, 0x000f   ;make B the number of blocks left in the last word of memory
SHR [A], B      ;set B number of blocks to 0 in the bitmap

SET B, POP ;restore B
SET A, POP ;restore the number of words needed for the bitmap

:heap.init.evenbitmap

;A is already the number of words needed for the bitmap
ADD A, I ;add the header offset
ADD A, 4 ;make A the location of the start of the first block

STI [I], 0x9EA9
STI [I], A
STI [I], C
STI [I], B

SET I, A   ;since A is already the starting location, 
ADD I, POP ;create ending location

:heap.init.loop ;clear backwards
IFE I, A
  SET PC, heap.init.exit
STD [I], 0
SET PC, heap.init.loop

:heap.init.exit
SET J, POP
SET I, POP
SET PC, POP
 ;

;:### heap.alloc (length)
;:Reserves the first consecutive *length* of memory in the heap.
;:
;:**returns** the location of the reserved memory, <strike>or 1 if no such block of memory is available</strike>
;:
:heap.alloc
SET PUSH, X
SET PUSH, I
SET PUSH, J

SET X, [env.heap] ;make X the heap location

SET B, [X+2] ;make B the block length
DIV A, B ;make A the number of blocks needed
IFN EX, 0
  ADD A, 1 ;round up
SET I, A ;make I the iterator for number of blocks needed

SET B, X ;make B the location of the header
ADD B, 4 ;add the length of the header to make B the location of the bitmap

SET A, [B] ;make A the first word of the usagemap

SET PUSH, I ;store iterator for easy access
SET J, 0 ;make J the iterator to keep track of the bit number

:heap.alloc.checkbits

IFE J, 16 ;if the last bit in the word has been scanned
  SET PC, heap.alloc.newword

IFC A, 0x8000 ;if the leftmost bit of A is 0,
  SET PC, heap.alloc.openbit

;if it's 1, note the closed bit
SET I, PEEK ;reset the iterator
SHL A, 1
ADD J, 1 ;increase bit iterator
SET PC, heap.alloc.checkbits

:heap.alloc.newword ;go to the next word
ADD B, 1 ;increase the pointer by 1

;SHOULD DETECT THE END OF THE BLOCK MAP

SET A, [B]
SET J, 0
SET PC, heap.alloc.checkbits

:heap.alloc.openbit
SUB I, 1
SHL A, 1
ADD J, 1 ;increase bit iterator

IFN I, 0 ;if the iterator has not reached zero, continue
  SET PC, heap.alloc.checkbits

SET C, X ;set C to the location of the header
ADD C, 4 ;set C to the location of the bitmap

SUB B, C ;set B to the number of full words passed
SHL B, 4 ;set B to the number of bits passed (MUL 16)

SUB J, PEEK ;subtract the number of needed blocks from the
            ;bit iterator to get the first index of the open area
            ;but keep the number of blocks needed for later

ADD B, J ;add the index of the first block in the open area
         ;in order to get the actual first block index

SET A, B   ;set A to the block index
SET B, POP ;set B to the number of blocks needed
SET C, 1   ;set C to "mark the blocks used"
JSR heap.mark

MUL A, [X+2] ;multiply by the length of each block
ADD A, [X+1] ;add the location of the first block in memory
             ;to produce the location of the first word of the first
             ;reserved block

SET J, POP
SET I, POP
SET X, POP
SET PC, POP
 ;

;:### heap.free (location, length)
;:Marks the blocks specified by *location* and *length* as free, and clears them.
;:
;:**returns** *location*
;:
:heap.free
SET PUSH, A

SET C, [env.heap]

SUB A, [C+1] ;subtract the first block's location from A
DIV A, [C+2] ;divide by the size of a block, to get the
             ;block index, rounding down

;DIV A, [C+2] not coming out evenly (EX != 0) implies corruption
;of some sort. Impose a check here?

DIV B, [C+2] ;make B the number of blocks implied by *length*
IFN EX, 0    ;rounding up, as the allocation subroutine does
  ADD B, 1

SET C, 0 ;"unused"
JSR heap.mark

SET A, POP
SET PC, POP
 ;

;:### heap.mark (block, number, used/unused)
;:Marks *number* of blocks used or unused, starting from the index *block*. `1` in *used/unused* denotes `used`, and `0` denotes `unused`.
;:
;:**return** *block*
;:
:heap.mark
SET PUSH, A
SET PUSH, X
SET X, [env.heap]
ADD X, 4 ;make X a pointer to the first word in the bitmap

SUB B, 1 ;for use later
SET PUSH, C ;store used/unused type
SET PUSH, B ;store B
SET PUSH, A ;store A

SHR A, 4 ;divide by 16, to get the word index
ADD A, X ;make A a pointer to the relevant word

SET B, POP ;restore the block index
AND B, 0x000f ;get the bit index

SET C, 0x8000 ;set the leftmost bit
ASR C, POP    ;make the leftmost *number* bits 1

SHR C, B ;make C apply to all of the blocks to be reserved

SET B, POP ;restore used/unused
IFE B, 1
  SET PC, heap.mark.used

:heap.mark.unused
XOR C, 0xffff ;flip the bits to reflect what needs to be changed
AND [A], C    ;set the bits that need to be changed to 0

SET X, POP
SET A, POP
SET PC, POP

:heap.mark.used
BOR [A], C ;set the bits that need to be changed to 1

SET X, POP
SET A, POP ;return *blocks*
SET PC, POP
 ;
 ;;
