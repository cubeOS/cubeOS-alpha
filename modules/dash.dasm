;;;;;;;;;;;;;;;;
; DASH ; Shell ;
;;;;;;;;;;;;;;;;
;:The DASH Shell is essential to cubeOS's function. It acts as an interpreter for ASCII commands, <strike>which are passed to the package manager in order to directly execute subroutines,</strike> and also handles keyboard input.
;:
;:## Usage
;:At the prompt, the user can use an attached Generic Keyboard to input ASCII and control characters. 
;:
;:## Control Characters
;:
;:### Control + Delete
;:Restarts the DASH Shell by calling *s.instance*.
;:
;:## Subroutines

:v.sPrompt DAT 0xF001, "$ ", 0

:dash.processMemory

:v.promptLoc DAT v.promptLoc - dash.processMemory
;offset to literal prompt location


:v.args DAT v.args - dash.processMemory
;offset of pointer  


:v.argc DAT v.argc - dash.processMemory
;offset to literal argc


:v.stdin DAT v.stdin - dash.processMemory
;offset to pointer to stdin


:v.stdout DAT v.stdout - dash.processMemory
;offset to pointer to stdout


:s.stdin.quoteflag DAT s.stdin.quoteflag - dash.processMemory
;offset to literal; is 1 if iterator is inside quotes

;:### s.instance
;:Prepares a new terminal session at the root directory.
;:
:s.instance
SET PUSH, Z ;for memory location storage

JSR tusk.newProcess
SET Z, A

JSR dview.newScreen ;get screen location

SET B, fontRAM ;get fontRAM location
JSR sv.init

SET A, 0x80
JSR tusk.getMem
SET [Z+v.stdin], A ;store the pointer in memory + v.stdin offset

SET A, 0x80
JSR tusk.getMem
SET [Z+v.stdout], A ;store the pointer in memory + v.stdout offset

;SET A, 0x120 ;384 words, for the screen
;JSR tusk.getMem
;SET [Z+v.screenLoc], A

JSR sv.clear ;clear the screen
SET A, [v.scrnLoc]

:s.instance.promptLoop
JSR s.takeIn
SET PUSH, A ;keep track of cursor location

JSR s.parse

SET A, POP ;restore cursor location for passing to s.stdout
JSR s.stdout

SET PC, s.instance.promptLoop

SET PC, POP
 ;
 
;:### s.takeIn (cursorPointer)
;:Writes a prompt to the screen using *v.sPrompt* and enters a loop to take ASCII input from the keyboard and write it to the screen at *cursorPointer*. It exits when the user presses `enter`.
;:
;:**returns** the cursor location on exit
;:
:s.takeIn
SET PUSH, I
SET PUSH, J
SET I, 0

JSR sv.getNewLine ;get the next blank line after *cursorPointer*
JSR s.drawPrompt ;draw the prompt using s.drawPrompt

SET A, 1 ;HWI parameter

:s.takeIn.loop
IFL I, [v.promptLoc]
  SET I, [v.promptLoc]
SET [I], 0xf09e ;cursor character

:s.takeIn.wait
HWI [HW.GenericKeyboard]

IFG C, 0x1f
  IFL C, 0x80
    SET PC, s.takeIn.write

IFE C, 0x10
  SET PC, s.takeIn.backspace

IFE C, 0x11
  SET PC, s.takeIn.enter
  
;INSERT (0x12)
  
IFE C, 0x13
  SET PC, s.takeIn.delete
  
SET PC, s.takeIn.wait


:s.takeIn.write
XOR C, 0xF000
STI [I], C

IFN I, [v.scrnEnd] ;write normally
  SET PC, s.takeIn.loop

:s.takeIn.write.scroll
SUB [v.promptLoc], [v.scrnWidth] ;make prompt scroll, too
JSR sv.scrollUp ;scroll everything up
SET PC, s.takeIn.loop


:s.takeIn.backspace
STD [I], 0
SET [I], 0
SET PC, s.takeIn.loop
 ;

:s.takeIn.delete
SET A, 2 ;changes interrupt action, so must be reset afterward
SET B, 0x91 ;sets B to the *Control* key
HWI [HW.GenericKeyboard] ;C becomes 1 if *Control* is pressed

IFE C, 1 ;exit if 
  SET PC, s.instance

SET A, 1 ;reset interrupt action
SET PC, s.takeIn.loop
 ;

:s.takeIn.enter
SET [I], 0
SET A, [v.promptLoc]
SET B, I ;UNNECESSARY ASSIGNMENT OF CURSOR POSITION
JSR s.stdin

SET A, I;return location in A
SET I, POP
SET J, POP
SET PC, POP
 ;

;:### s.parse
;:This subroutine isolates the first argument in *v.stdin* by scanning through, then stopping on null or 0x20. Then, it gets the *crp.sum* of the string. Then, it calls *p.lookup*, sets `B` to the location of stdin and `C` to the location of stdout, and invokes the subroutine returned by *p.lookup*.
;:
;:This should also point the process to the starts of the remaining arguments.
;:
:s.parse
SET A, [Z+v.stdout] ;prepare stdout by nullifying the first word
SET [A], 0x0000   ;to overwrite old output if it exists

SET A, [Z+v.stdin] ;make both A and B the location of stdin
SET B, A         ;for length-finding later

:s.parse.getFirstArg
IFC [B], 0x20 ;if [B] is 0x0000 or 0x0020
  SET PC, s.parse.exit
ADD B, 1
SET PC, s.parse.getFirstArg

:s.parse.exit
SUB B, A ;make B the length of the first argument
JSR crp.sum ;returns the hashvalue in A

JSR p.lookup

SET PUSH, I
SET PUSH, J
SET PUSH, X
SET PUSH, Y
SET PUSH, Z
IAG B
SET PUSH, B ;push IA

SET X, A ;jump to X
SET A, [v.argc]   ;argc
SET B, v.args     ;pointer to argv
SET C, [Z+v.stdout] ;output location

JSR X ;calls the subroutine returned by *p.lookup*

IAS POP
SET Z, POP
SET Y, POP
SET X, POP
SET J, POP
SET I, POP

;SET A, 0
;SET B, [v.scrnLoc]
;HWI [HW.LEM1802]

SET PC, POP
 ;

;:### s.drawPrompt (pointer)
;:Draws a prompt using *env.cwds* and *v.sPrompt* at *pointer* and sets *v.promptLoc* to the location following the prompt.
;:
;:**returns** the location following the prompt
;:
:s.drawPrompt

SET B, A
SET A, v.sPrompt
JSR sv.write

SET [v.promptLoc], A

SET PC, POP
 ;

;:### s.stdin (startPointer)
;:Performs a C-style copy from *startPointer* to *v.stdin*, replacing un-(single)quoted spaces with zeros.
;:
:s.stdin
SET PUSH, I
SET PUSH, J
SET PUSH, X
SET [Z+v.argc], 1 ;set argc to 1 (including the zeroth argument)
SET I, A
SET X, 1 ;the argument indexing index ($1, $2)
SET B, [Z+v.stdin]

SET [v.args], B ;handle $0 separately

:s.stdin.loop
STI [B], [I]
AND [B], 0x007F

IFE [B], 0x27 ;single quote
  XOR [Z+s.stdin.quoteflag], 1 ;flip state
  
IFN [Z+s.stdin.quoteflag], 1 ;unless they're quoted
  IFE [B], 0x20 ;set spaces
    JSR s.stdin.newarg

ADD B, 1

IFN [I], 0
  SET PC, s.stdin.loop

SET [B], 0
IFL X, 15
  SET [v.args + J], 0 ;zero the pointer following the last argument
SET X, POP
SET J, POP
SET I, POP
SET PC, POP

:s.stdin.newarg
ADD [Z+v.argc], 1
SET [B], 0
SET C, B
ADD C, 1
IFL X, 15
  SET [v.args + X], C
ADD X, 1
SET PC, POP



;:s.stdin.escflag DAT 0 ;is 1 if "\" was the most recent character

;:s.stdin.args ;for handling arguments
;DAT 0,0,0,0, 0,0,0,0, 0,0,0,0, 0,0,0,0
 ;

;:### s.stdout (outputPointer)
;:Does a C-style copy from *v.stdout* to *outputPointer*.
:s.stdout
JSR sv.getNewLine

SET B, A ;output
SET A, [Z+v.stdout] ;input
SET PC, sv.write ;process to screen (null-terminated)
 ;
;;RESERVED SPACE

;for keeping track of where arguments are


:v.cwd ;current working directory sector number
DAT 0
:v.cwds ;current working directory in ascii
DAT 0x202f, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0
 ;;
